start: "@startuml" (class | relationship)* "@enduml"

// Classes
class: "class" class_name ("{" (attribute | method)* "}")?

variable: (visibility? var ":" type)
        | (visibility? type var)

?attribute: variable

param_list: [variable ("," variable)*]

method: (visibility? method_name "(" param_list ")" ":" type)
      | (visibility? type method_name "(" param_list ")")

// Relationships
relationship: arrow_end_1 cardinality? arrow_head_1 arrow_body arrow_head_2 cardinality? arrow_end_2 [":" relation_text]

// Arrow Heads
!arrow_head_1: "<"
             | "<|"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_head_2: ">"
             | "|>"
             | "*"
             | "o"
             | "+"
             | "#"
             | "x"
             | "}"
             | "^"
             | " "

!arrow_body: ("-"
           | "--"
           | "-left-"
           | "-up-"
           | "-right-"
           | "-down-"
           | "."
           | ".."
           | ".left."
           | ".up."
           | ".right."
           | ".down." )

cardinality: ESCAPED_STRING

// Variables and Methods visibility
visibility: "+" -> public
          | "-" -> private
          | "#" -> protected
          | "~" -> package

arrow_end_1: CNAME
arrow_end_2: CNAME
type: CNAME
var: CNAME
attribute_name: CNAME
method_name: CNAME
class_name: CNAME
relation_text: (WORD|ESCAPED_STRING|DIGIT|">"|"<")*

%import common.CNAME
%import common.ESCAPED_STRING
%import common.WORD
%import common.DIGIT
%import common.WS
%ignore WS
